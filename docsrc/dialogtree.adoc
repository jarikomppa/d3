# DialogTree (D3)
Jari Komppa, https://iki.fi/sol
:doctype: book
:imagesdir: ./images
:toc:
:toclevels: 4
:data-uri:

# Introduction

DialogTree is a engine for building dialogue trees with non-player characters in games. The way it works, it can also be used as basis
for multiple choise adventures, visual novels and the like. Just be careful not to use the trademarked terms enforced by ChooseCo.

The d3 concept consists of several pieces. Each dialogue itself is called a "deck", where each scene is a "card" which contains a "question" 
field and zero or more "answer" fields. Each answer then tells which card should be read next.

image::justagraph.png[Just a flow chart,align="center"] 

Think of each box as a page in a book, and at the bottom of each page you'll find something in the lines of, "Turn to page 32 if you want to..".

These "decks" are written in DialogTree story definition language. Large portion of this document covers this process. For most uses,
the format is trivial, but DialogTree offers functionality to make very complicated dialogues.

The "deck" files are compiled to usable form with the d3 compiler. The resulting files are read by the d3 engine which can be integrated
to your actual game engine, along with some form of persistent state.

## Concepts

Here's some of the terms used by DialogTree.

[cols="20,~"]
|===
|Term      | Description

| Card     | One set of question and zero to multiple answers 
| Deck     | Set of cards 
| Symbol   | Variable (containing whole numbers), a flag (on or off) or card identifier 
| Variable | Integer (whole number) variable, used for complex logic 
| Flag     | Boolean (on or off), used for simple logic 
|===


## Legalese

DialogTree is released under Unlicense, which is basically public domain.

<https://github.com/jarikomppa/d3>

This is free and unencumbered software released into the public domain.

Anyone is free to copy, modify, publish, use, compile, sell, or
distribute this software, either in source code form or as a compiled
binary, for any purpose, commercial or non-commercial, and by any
means.

In jurisdictions that recognize copyright laws, the author or authors
of this software dedicate any and all copyright interest in the
software to the public domain. We make this dedication for the benefit
of the public at large and to the detriment of our heirs and
successors. We intend this dedication to be an overt act of
relinquishment in perpetuity of all present and future rights to this
software under copyright law.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.

For more information, please refer to 

<http://unlicense.org/>

# DialogTree Story Definition Language

The DialogTree story definition language may sound scary, but it is actually very simple. The DialogTree scripts are plain text files, such as those that can be created with Windows Notepad, although the author recommends using something more advanced, such as Visual Studio Code, Sublime Text or Notepad++.

Not all of the features of the language are needed to write adventures; this document is structured so that more complicated features are described later, so you won't need to read all of it to get started. If something feels complicated, don't worry about it - you can come back to it later.

## Structure

Structure of a multiple choice adventure is based on cards (other used terms include pages, rooms, locations, nodes). Each card consists of description part as well as zero or more choices for the user (also known as answers, selections, options).

Consider the computer asking the user a question, and offering several possible answers for the question.

The script for a simple page looks like this:

    $Q roomname
    The description of the room
    $A otherroomname
    Go to other room
    
The choices link pages together. Here we'll describe three rooms and let the user travel between them:

image::rgbroom.png[Flow chart of the rgbroom,align="center"]

    $Q redroom
    You're in the red room.
    $A greenroom
    Go to the green room
    $A blueroom
    Go to the blue room
    
    $Q greenroom
    You're in the green room.
    $A blueroom
    Go to the blue room    
    $A redroom
    Go to the red room
    
    $Q blueroom
    You're in the blue room.
    $A greenroom
    Go to the green room
    $A redroom
    Go to the red room
    

Everything between two $Q lines goes to a single card. The first word after the $Q and $A is the identifier of the room. In programming terms, you could think of it as GOTO label. Two rooms may not have the same identifier, but any number of $A lines may point to the same identifier. Identifiers may not include spaces (i.e, they need to be a single word).

It is also perfectly legal for a room to have an choice which goes back to the same page. This is actually rather useful.

If a card has no $A lines, it is considered an "end node", and the dialogue ends if player ends up on that page.

### Text

The text in the page descriptions is automatically formatted, removing any unnecessary "white space" like line breaks or additional spaces. To start a new paragraph, add an empty line in your text.

    All of
    these go
    on the
    same line.
    
    This is a new paragraph.
    
        Adding   more   spaces   does   nothing.

To add an empty line, use the $P line (see below).

### Comments

You can add comments in the script which do not end up in your adventure file by starting a line with the hash character.

    # This is a comment
    This is not a comment    

### Paragraph Breaks

While the text is expected to flow by default, sometimes empty lines may be desirable. To output an empty line, use the $P statement.

    $Q fountain
    You arrive at a picturesque fountain. Assume there's a long description here.
    $P
    You can see some mushrooms here.
    
## Flags

In addition to the basic structure of the cards, flags can be used to introduce more complicated behavior. By default, every time the player goes to a card, the flag with the card's name is turned on. Using these, an optional part can be added to a card's description.

    $Q alleyway
    You're in a dark alley. There's a door.
    $P backyard
    You remember seeing the bad guys inside when you were at the back yard.

In this example the "You remember.." line is only shown if the player has visited the backyard page before ending up here.

If you wish to have optional part of the text between two parts of text that is always shown, you can reset the optionality by using an empty $P line.

    Text always shown
    $P daytime
    Text shown only during daytime
    $P
    Another text always shown

If you want to use optional paragraphs but do not want the newline, you can use $O in place of $P. In more complicated cases this can get a bit messy though, so it's usually a good idea to keep things simple.
    
### Checking Flags

You can check if a flag is on by using its label. You can also check if a flag is not on by prefixing the flag's label with an exclamation point (!), like !backyard.

    $P daytime
    The sun is shining.
    $P !daytime
    The moon is bright tonight.
    
Alternate syntax for this is to use the has: and not: prefixes.

    $P has:daytime
    The sun is shining.
    $P not:daytime
    The moon is bright tonight.

If you wish something to happen randomly, you can use the rnd: prefix with a value.

    $P rnd:25
    Thorin sits down and starts singing about gold.
    
The maximum value is 100, so a value of 50 gives about 50% chance, etc.

The flag checks can also be used with the $Q and $A lines.
    
If flag check is used with the $A line, the choice is only given to the player if the flag check succeeds.

    $A mine has:cleared
    Enter the mine through the cleared tunnel.

If flag check is used with the $Q line, any other commands on that same line are only executed if the flag check is positive. (See below for the other commands). The card itself is still displayed.

### Setting Flags

To set a flag, use the set: prefix with a flag label.

    $A alleyway set:trapped
    Place the trap just outside the door.
    
If used with the $A line, the command is performed if the player picks the line. If used on a $Q, $I or $O line, the command is performed if the line's flag check succeeds. 

Setting a flag that is already on is legal, but has no effect.

### Clearing Flags

You can also clear flags, including the ones set by visiting a page. This is done with the clear: or clr: command.

    $A alleyway clear:trapped
    Change your mind, and clear the trap from the door.
    
Clearing a flag that is not on is legal, but has no effect.
    
### Toggling Flags

Flags can be toggled with the toggle: command. This way you don't need to know which state the flag is in, if you wish to switch between two states.

    $Q busystreet toggle:trafficlights
    $P trafficlights
    The traffic lights are red.
    $P !trafficlights
    The traffic lights are green.

    $A busystreet
    Wait for a while
    $A sleepytown !trafficlights
    Cross the street

## Numbers

Sometimes it's useful to handle numbers instead. Many gamebooks have concept of hit points, for instance.

### Setting Numbers

To set the value of a number, use the = operator.

    $A fountain hitpoints=7
    Drink from the fountain

You can set a number variable to the value of another variable or to a fixed value.

    $A tavern temp=player_money player_money=stranger_money stranger_money=temp
    Swap purses with the stranger

Note that there must be no spaces around the operators.

### Adding and Subtracting Numbers

Adding and subtracting are done with the - and + operators.

    $A fountain hitpoints-1
    Eat a mushroom
    $A fountain hitpoints+1
    Eat a biscuit
    
Again, fixed values or other variables can be used.

    $A fountain hitpoints+potion potion-1 potion>0
    Drink from the healing potion
    
If you prefer, you can use -= and += instead of - and +:

    $A fountain score+=3 darts-=1 darts>0
    Throw a dart at the board

### Comparing Numbers

Numeric variables can be compared in various ways, to each other and to fixed numbers.

    $O a==42
    a is 42
    $O a!=42
    a is not 42
    $O a>42
    a is bigger than 42
    $O a>=42
    a is bigger or equal to 42
    $O a<42
    a is smaller than 42
    $O a<=42
    a is smaller or equal to 42
    
Note that a=1 means "assign 1 to a", while a==1 means "is a equal to 1".

As you might expect, it's also possible to compare two variables.

    $O a==b
    a is equal to b
    $O a!=b
    a is not equal to b
    $O a>b
    a is bigger than b
    $O a>=b
    a is bigger or equal to b
    $O a<b
    a is smaller than b
    $O a<=b
    a is smaller or equal to b

Here again, a=b means "assign a to value of b" and a==b means "is a equal to b".

When comparing a varialbe with a number, the variable must always be the first parameter, i.e, you can't do 3==pi.

### Printing Numbers

It is also possible to print out the values of the numeric variables, by simply putting
the variable name between << and >> in the text. Note that there must be no spaces between
these characters.

    $Q store
    The shopkeeper polishes an apple while he's waiting for you
    to make a selection. You currently have <<gold>> gold.
    $A store gold>5 gold-5 set:dagger
    Buy the dagger for 5 gold

Alternatively it's possible to use the print command:

    You have
    $O print:gold
    gold pieces.

This is basically what the compiler turns the "inline" number printing to. This can cause
issued with voiceover and/or localization efforts, so use the number printing with caution.
    
### Mixing Flags and Numbers

Numbers and flags do not mix. If you try to assign flag to a numeric variable, for instance,
it won't do what you expect:

    $O set:parrot
    $O bird=parrot
    
This will create a new numeric variable called "parrot", which will live alongside the flag "parrot". The compiler will warn you if you try to do this.

## Go and Gosub

Sometimes it is useful to interrupt the normal page flow and do something else for a change.

For example, if your game has a hit point mechanism, it would be wasteful to add checking if the player has died on every single page.

    $Q healthcheck
    $P hitpoints==0 go:dead
    $P hitpoints<5
    You're not feeling too good.
    
    $Q fountain
    The fountain.
    $O gosub:healthcheck
    You find yourself near a marble fountain in the forest 
    clearing. There are some mushrooms nearby.

In the example above, whenever the player arrives at the "fountain" page, the game will load the healthcheck page, which will
first check if player is dead, and if so, will turn to the "dead" page immediately. If the player is still alive, the 
page will output the "You're not feeling too good" message if hitpoints are low. Otherwise the processing of that sub-page
is done and drawing of the "fountain" page resumes from where we were at.

Subpages can also not have any $A statements; if any exist, they will be ignored.

## Deck Configuration

DialogTree has special statement that can be used to configure the way the deck works.

    $C prefix
    
The first parameter to the $C statement is the deck's prefix. All of the local symbols in the deck are prefixed with
this string before they're sent to the persistent state. The prefix can be followed by other options. Currently only
two options exist:

[cols="20,~"]
|===
|Option          | Description

|noimplicitflags | Disable flag setting upon entering a card
|implicitflags   | Enable flag setting upon entering a card
|===

By default, DialogTree sets a flag with the same name as the card. This behavior can be enabled or disabled. If the
option is enabled or disabled on command line, this option overrides it.

## Global Pages

Sometimes it's useful to have a card that is evaluated at the start of every other card.

    $G symbol
    
This is functionally identical to having gosub:symbol at the start of every other $Q statement.

## More Technically Speaking..

Here's some a bit more technical notes which may be useful in problematic cases.

### Command Execution Order

To get a little bit more nitty-gritty, here's a few words about command execution order.

Commands are, generally speaking, executed in the order they're set. In the following example, foo is set to 1, and then we go to
another page, which means the foo=2 instruction is never executed:

    $O foo=1 goto:elsewhere foo=2
    
However, flag check is always performed first, and only if the whole flag check succeeds, the other commands
are executed. Thus, if you write something as convoluted as:

    $O set:flaggy flaggy clear:flaggy toggle:flaggy
    
what happens is:

    If flaggy is on:
        Set flaggy
        Clear flaggy
        Toggle flaggy

Another example, just to be sure:

    $O attr=7 apple cls=1 banana rnd:25 orange set:strawberry
    
This becomes:
    
    If apple is on, and banana is on, 
    and random is less than 25 and orange is on:
        Set attr to 7
        Set cls to 1
        Set strawberry

To reiterate: first everything that affects whether the line should be executed is
evaluated, and only then the rest are executed, assuming all of those things turn
out to be true.

If any of the checks fail, the rest are not executed.

In case of $A blocks, the answer is included if it produces any text (i.e, if there is
at least one paragraph that is not predicated away), but the commands are only executed
when the option is selected.

### Go and Gosub Revisited

The exact point at which the go and gosub commands are executed is exactly where the opcode is:

    $Q mysubpage
    Hello
    
    $Q normalpage
    $O gosub:mysubpage
    World

Outputs "Hello World".

Using the go: command will send the player to a new page just as if they had selected an option: the room's flag will be set, the screen
cleared, etc. The jump happens at the place where the opcode is, meaning that the rest of the opcodes (if any) as well as the rest of the
page, including any $A blocks, is ignored.

### The $P vs the $O Statement

The $O and $P statements work exactly the same, except that when the compiler finds an $O statement, it will look back to the previous paragraph and will remove any newlines from the end of it. This works fairly well in simple cases, but if you find yourself chaining several predicated $O and $P blocks, things may get confusing.

### Using Several Source Files

It is possible to split source files into several pieces and combine them with the $I statement. If you find you want to include some piece
of text (or logic!) in several decks, this can be done by using this include statement.

    The troll takes a deep breath, and says:
    $I legalese.txt
    
The $I statement works exactly as if you copy-pasted the file on that line.

### Using Several Decks

Since all decks talk with the same persistent state, keeping track of the symbols can be rather difficult. Every deck can have a prefix set
using the config statement:

    $C prefix
    
This will turn every local symbol to a global one. For example, if you have a deck with prefix "joe", and you set the symbol "grateful", the
symbol that actually gets set in the state is called "joe.grateful". If you refer to a symbol with a period in it, the prefix is not set.
Thus, you can take your deck with prefix "lisa" and check if joe is grateful by referring to the "joe.grateful" tag.

It is also possible to have several decks with the same prefix. This can be useful if you have several discussions with the same NPC, and don't
want to put all of the text into a single source file.

## Reference

Here's all of the syntax in handy table form. In many cases there are several ways to do the same thing so you can pick your favorite syntax. All of the variants compile to the same thing.

### Line Types

[cols="20,~"]
|===
|Line start    | Description
|$Q            | Start of a new card. Can include opcodes (commands and predicates)
|$A            | Answer block. Can include opcodes (commands and predicates)
|$P            | Paragraph. May contain opcodes (commands and predicates)
|$O            | Optional. Same as $P but without paragraph break.
|$C            | Deck configuration. Use to set deck prefix and optional flags.
|$I            | Include another file into the script
|$G            | Global page. Works like $Q, included at start of every $Q
|===

### Predicates

Predicates control whether the rest of the commands are executed, as well as control whether an answer is shown to the user.

#### Flags

[cols="20,~"]
|===
|Opcode      | Description

|has:symbol  | Check if a symbol is set
|need:symbol |
|symbol      | 
|not:symbol  | Check if a symbol is not set
|!symbol     | 
|===

#### Random

[cols="20,~"]
|===
|Opcode       | Description

|random:value | Take random number in 0..99 range and compare it to given value
|rand:value   |
|rnd:value    |
|===

#### Numeric

[cols="20,~"]
|===
|Opcode         | Description

|symbol==value  | Check if variable is equal to value
|symbol==symbol | Check if variable is equal variable
|symbol!=value  | Check if variable is not equal to value
|symbol!=symbol | Check if variable is not equal variable 
|symbol<value   | Check if variable is less than value 
|symbol<symbol  | Check if variable is less than variable 
|symbol<=value  | Check if variable is less or equal than value 
|symbol<=symbol | Check if variable is less or equal than variable 
|symbol>value   | Check if variable is greater than value 
|symbol>symbol  | Check if variable is greater than variable 
|symbol>=value  | Check if variable is greater or equal than value 
|symbol>=symbol | Check if variable is greater or equal than variable 
|===

### Commands

Commands are used mostly to modify the state of variables and flags, but there are other uses too.

#### Flags

[cols="20,~"]
|===
|Opcode         |Description                    

|set:symbol     | Set the symbol                
|clear:symbol   | Clear the symbol              
|clr:symbol     |
|toggle:symbol  | Toggle the symbol on/off      
|xor:symbol     |                              
|flip:symbol    |                          
|===

#### Control Flow

[cols="20,~"]
|===
|Opcode         |Description                    

|go:symbol      | Go to the card with this symbol 
|goto:symbol    |                               
|gosub:symbol   | Visit the card with this symbol, then come back 
|call:symbol    |                               
|===

#### Numeric

[cols="20,~"]
|===
|Opcode         |Description                    

|print:symbol   | Print the value of variable 
|symbol=value   | Set numeric value to variable   
|symbol=symbol  | Copy numeric value of variable to variable 
|symbol+value   | Add numeric value to variable   
|symbol+=value  |                               
|symbol+symbol  | Add numeric value of variable to variable 
|symbol+=symbol |                               
|symbol-value   | Subtract numeric value from variable 
|symbol-=value  |                               
|symbol-symbol  | Subtract numeric value of variable from variable 
|symbol-=symbol |                               
|symbol/value   | Divide variable with numeric value 
|symbol/=value  |                               
|symbol/symbol  | Divide variable with variable 
|symbol/=symbol |                               
|symbol*value   | Multiply variable with numeric value 
|symbol*=value  |                               
|symbol*symbol  | Multiply variable with variable 
|symbol*=symbol |                              
|===

Note: division by zero is considered a non-operation.

## Example Snippets

Here's a few example snippets of more complicated behaviors that are possible with flag manipulation.

### Button Toggle

    $Q room
    $P !light
    Light is off
    $P light
    Light is on
    $A room toggle:light
    Toggle light

This example has a single room with a light that can be on or off. The player's option always points back to the same room, and the optional text blocks show whether the light is on or off.

The same flag could be used for other things, such as revealing other things to do if the light is on.

### Using Room Flags to Change Flavor Text

image::flavor.png[Flow chart of the flavor example,align="center"] 

    $Q start
    Go to cave or forest?
    $A cave
    Go to cave
    $A forest
    Go to forest
    
    $Q cave
    Well, go on..
    $A end
    Exit cave
    
    $Q forest
    Well, go on..
    $A end
    Exit forest
    
    $Q end
    $P cave clr:cave
    As you exit the cave, you find...
    $P forest clr:forest
    As you exit the forest, you find...
    $P
    ...the end of the example.
    $A start
    Restart

This example shows two different routes to a room, and the room's description reacts to where you came from. Note that the room flags are also cleared when the flavor text is shown, so it won't be shown again, should the player end up back on the same page from some other direction later on.

### Lock and Key

image::lockandkey.png[Flow chart of the lock and key example,align="center"]

    $Q room
    You're in a room with a door.
    $P !key
    There is a key here.
    $P open
    The door is open.

    $A room !key set:key
    Get key
    $A room key !unlocked set:unlocked 
    Unlock door
    $A locked !unlocked !open
    Open door
    $A room unlocked !open set:open
    Open door
    $A end open
    Exit
    
    $Q locked
    The door is locked.
    $A room
    Dang it!
    
    $Q end
    You went through the door.

In this example the player needs to take a key, unlock door, open door, and leave. When the player initially
reaches this page, the view looks like this:

    You're in a room with a door.
    There is a key here.
    ----
    Get key
    Open door

The "open door" option shown here does not actually open the door, but sends the player to a page that just says "The door is locked" with the only option of returning back to the page.

If the "Get key" option is picked, the player is sent back to the same page, but with the "key" flag enabled. Now the page looks like this:

    You're in a room with a door.
    ----
    Unlock door
    Open door

The "Open door" option is still the same "The door is locked" option. Hitting the "Unlock" option again sends the player back to the same page, now with the "unlocked" flag on.

    You're in a room with a door.
    ----
    Open door

Now the "Open door" option does something different (it's a different "Open door", after all); sending the player back to the same page with the "open" flag on.

    You're in a room with a door.
    The door is open.
    ----
    Exit
    
Finally, the player is given the "Exit" option, which sends the player to the end node, where the game ends.

### Separate Gate

image::separategate.png[Flow chart of the separate gate example,align="center"]

    $Q rivercrossing
    River flows north to south. There's signs of people 
    having crossed the river here.
    $P !dam
    The flow is too strong for you to pass.    
    $A forestpath
    Go north
    $A end dam
    Cross the river
    
    $Q forestpath
    You're on a north-south forest path. There's a river
    to the west.
    $A rivercrossing
    Go south
    $A upstream
    
    $Q upstream
    This is as far north as you can go. There's a forest 
    path south, and a river to the west.
    $P !dam
    It seems you could easily roll a big stone to the river,
    $P dam
    Huge rock is blocking the river flow.
    creating a dam.
    $A forestpath
    Go south
    $A upstream set:dam
    Roll that rock.
    
    $Q end
    You crossed the river.
    
This example uses several locations. The player has to travel upstream to
create a dam in order to be able to cross the river.

### Simple Dialogue

The description-choices structure can also describe dialogue with a non-player character.

    $Q start
    Well hello there. What do you want to know about?
    $A name
    Name
    $A jobs
    Jobs
    $A virtue
    Virtue
    
### Sticky Randoms

In order to keep the result of a random decision, set the result in a flag.

image::stickyrandoms.png[Flow chart of the sticky randoms example,align="center"] 

    $Q coinroom
    $P !toss clr:result
    $P !toss rnd:64 set:result
    $P set:toss
    $P result
    The coin shows heads.
    $P !result
    The coin shows tails.
    
    $A coinroom
    Look again.
    $A coinroom clear:toss
    Toss again.

In pseudocode, the above becomes:

    If "toss" is not set:
        Clear "result"
    If "toss" is not set, AND random is less than 64:
        Set "result"
    Set "toss"
    If "result" is set
        Print "The coin shows heads."
    If "result" is not set:
        Print "The coin shows tails."

### Nerdy Boolean Logic

Freely ignore this if you don't consider yourself a nerd.

In order to AND two flags, simply check both of them at the same time:

    $O this that
    
In order to OR two flags, check them separately and set a third flag

    $O this set:thisorthat
    $O that set:thisorthat
    $O thisorthat
    
In order to exclusively-or two flags, you can use toggle.

    $O clr:thisxorthat
    $O this toggle:thisxorthat
    $O that toggle:thisxorthat

### Swapping Values

A temporary variable can also be used to swap contents of two numeric variables.

    $A tavern temp=player_money player_money=stranger_money stranger_money=temp
    Swap purses with the stranger

Swapping the state of two flags can also be done through a temporary flag, but can not be done on one line.

    $O clr:t
    $O a set:t clr:a
    $O b set:a clr:b
    $O t set:b
    
## Examples

Here's short overview of some of the examples included in the kit.

### Simple

image::simple.png[Flow chart of the Simple example,align="center"] 

The simple example has four pages, with links between them. None of the more 
advanced features of DialogTree are used.

### Complex

image::complex.png[Flow chart of the Complex example,align="center"] 

The complex example concentrates on the more advanced features of DialogTree, including
flag manipulation, and primarily has one heavily self-referential page.

The player needs to put on a light in order to see a key, which the player must pick up,
unlock a door, open the door, and then leave.

### Traveller

image::traveller.png[Flow chart of the Traveller example,align="center"] 

In the traveller example the player is expected to run after a fleeing creature, which
hops from one room to the next randomly, with a small chance of it staying put.

This effect is created through a relatively complicated logic. Each room consists of the
following kind of structure:

    $P clr:flip clr:stay
    $P rnd:50 set:flip
    $P rnd:20 set:stay
    $P red !stay flip clr:red set:green
    You see Yellow run towards the green room.
    $P red !stay !flip clr:red set:blue
    You see Yellow run towards the blue room.
    $P red
    You see a wild Yellow here.

First, "flip" and "stay" flags are cleared. Next, "flip" is set 50% of the time, and "stay" is set rather rarely.

Then, if the creature is in this room and is not staying, depending on whether flip is on, the creature is moved
from this room to one of the other ones.

If the creature is still in this room after those checks, we announce it.

### Waiting

image::waiting.png[Flow chart of the Waiting example,align="center"] 

This is the complete "Waiting for the Light" gamebook by Kieran Coghlan, consisting of
a hundred locations, flags and numbers, converted to MuCho, the zx spectrum predecessor of DialogTree. 
The MuCho version was done with persmission of the author. 

To understand the structure, let's look at some sub-graphs.

image::waiting_base.png[Flow chart of the Waiting example: base pages,align="center"]

The base structure is as follows: we start, have a couple simple trap choices, then arrive at the hall of lights (page 49), visit some other segments but eventually pick the white light (page 25) which checks if we've gone through all the other segments (via checking the various items), and then presents one of the 16 endings depending on the player choices in the other colored light segments.

image::waiting_green.png[Flow chart of the Waiting example: green pages,align="center"] 

The green segment is relatively simple (in context of this gamebook). Note that the player can die here in a few ways (p30). In the end, the player can either be kill the beast or not (pages 15 and 16).

image::waiting_pink.png[Flow chart of the Waiting example: pink pages,align="center"] 

The pink segment represents a relatively complex dialogue. Basically you get to recommend various options, one being a good one and the rest are less so.

image::waiting_red.png[Flow chart of the Waiting example: red pages,align="center"]

The red segment is again relatively simple, in the end you get to pick whether to be aggressive or not.

image::waiting_yellow.png[Flow chart of the Waiting example: yellow pages,align="center"] 

The yellow segment is super complicated: it's a game show. Not only can you answer right or wrong, or let the opponent answer instead, if you get the answers right, you can either take a point or take a card that has various results. And if either you or your opponent reaches 3 points, you hop to an ending.

This uses a subpage to check (and display) the scores and a couple numeric variables to keep the score.

The dialogtree version is actually more difficult than the paper version because the player keeps track of the variables in the book version; the computer version hides them, so you don't know which result you're getting from your choices until the end.

### Blackjack

image::blackjack.png[Flow chart of the Blackjack example,align="center"] 

The Blackjack example demonstrates some complex handling of numeric variables. The rules are a bit simplified as compared to actual blackjack; there's no actual card deck, and cards are valued from 0 to 10.

After the intro page the player arrives at the start page where they can choose how much to bet. Note that the game makes sure the player never bets more than they have.

Once a non-zero amount is bet, the player can proceed to the ingame page.

The ingame page does sub-page calls to hit and dealerhit, checks for busts, and asks if player wants to hit or stand, if neither player has gone over 21. If one (or both) players have gone over 21, the player is sent to win, bust, or bothbust pages.

In the stand page there are multiple calls to dealerhit, to make sure the dealer has reached 17 or more. Then the results are checked, and the player is sent to win, bust or bothbust pages. Bothbust is used in case of a tie.

In the bothbust page the player gets the bet back, and is sent back to start to bet again. In the bust page the bet is lost, and we check if player has run out of money, and if so, end the game. There's no separate game over page, we simply won't show the option to start over.

In the win page the player gets their money back twice, and we carefully check if the sum goes over maximum. If it does, we show the good ending text and end the game there. There's no separate game over page in this case either.

The hit and dealerhit pages generate 4 random flags, and then produce a value between 0 and 10 to add to either the player's, or the dealer's, score. Since there are 16 options, 3 of the options give 10 points and 4 of the options give 0 (9+4+3=16). (There's no looping in DialogTree (yet, anyway), so we can't just ignore the zero results and randomize again).

And there you have it - something that approximates blackjack in DialogTree.

# Engine Integration

The DialogTree engine has been implemented in several programming languages, but the basic idea for all is the same; to make using the 
dialogs as simple as possible. The interfaces follow this pattern:

- Allocate and connect to persistent state
- Load / Use data
- Get question text
- Get number of answers
- Get answer text
- Choose answer
- Cleanup

## Python Engine

tbd

## C Engine

tbd

## C++ Engine

tbd

## JavaScript Engine

tbd
